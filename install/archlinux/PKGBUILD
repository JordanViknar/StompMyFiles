# Maintainer: JordanViknar

pkgname=stomp-my-files-git
pkgver=r2.5abd7f2
pkgrel=1
pkgdesc="Universal file compression utility, aiming for brute strength (at the cost of time) and ease of use."
arch=('x86_64' 'i686' 'armv7h' 'aarch64')
url=https://github.com/JordanViknar/StompMyFiles
license=(MPL-2.0)
depends=(
	'lua'
	'lua-filesystem' # Used for some filesystem operations
	'lua-socket' # Used for pings and downloads
	'lua-sec' # Used for secure https downloads
	'which'
	'tar' # Used to unpack downloads
	'grep'
	'xz'
)
optdepends=(
	'p7zip: Use 7z system installation for compression'
	'dolphin-emu: Enable Wii/GameCube compression through dolphin-tool'
	'nsz: Use NSZ system installation for compression'
)
makedepends=(git)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("StompMyFiles::git+https://github.com/JordanViknar/StompMyFiles")
md5sums=(SKIP)

pkgver() {
	cd "$srcdir/StompMyFiles"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	app_directory="${srcdir}/StompMyFiles"
	install_directory="${pkgdir}/usr/share/stomp-my-files"

	install -D "${app_directory}/smf" "${pkgdir}/usr/bin/smf"
	install -D "${app_directory}/smf.lua" "${install_directory}/smf.lua"
	cp -r "${app_directory}/modules" "${install_directory}/modules"
	cp -r "${app_directory}/extra" "${install_directory}/extra"
	cp -r "${app_directory}/compressors" "${install_directory}/compressors"
}
